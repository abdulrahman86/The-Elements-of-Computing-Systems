// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
    // Put your code here.
	Mux16(a=x, b=false, sel=zx, out=ZXANSWER); // ZX

	Not16(in=ZXANSWER, out=NEGAX);
	//Inc16(in=notx, out=NEGAX);
	Mux16(a=ZXANSWER, b=NEGAX, sel=nx, out=NXANSWER); // NX

	Mux16(a=y, b=false, sel=zy, out=ZYANSWER); // ZY

	Not16(in=ZYANSWER, out=NEGAY);
	//Inc16(in=noty, out=NEGAY);
	Mux16(a=ZYANSWER, b=NEGAY, sel=ny, out=NYANSWER); // NY

	Add16(a=NXANSWER, b=NYANSWER, out=ADDERANSWER);
	And16(a=NXANSWER, b=NYANSWER, out=ANDANSWER);
	Mux16(a=ANDANSWER, b=ADDERANSWER, sel=f, out=FANSWER); // F

	Not16(in=FANSWER, out=NEGAF);
	//Inc16(in=notf, out=NEGAF);
	Mux16(a=FANSWER, b=NEGAF, sel=no, out[0..7]=ZRCH1, out[8..15]=ZRCH2, out[15]=ng, out=out); // NO + NG

	// ZR:
	Or8Way(in=ZRCH1, out=ZR1);
	Or8Way(in=ZRCH2, out=ZR2);
	Or(a=ZR1, b=ZR2, out=NOTzr);
	Not(in=NOTzr, out=zr);

		
}
